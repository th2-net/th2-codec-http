plugins {
    id 'com.palantir.docker' version '0.25.0'
    id "com.jfrog.bintray" version '1.8.5'
    id 'com.github.harbby.gradle.serviceloader' version '1.1.5'
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'application'
}

ext {
    sharedDir = file("${project.rootDir}/shared")
    sailfishVersion = '3.2'
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()
    maven {
        name 'Bintray'
        url 'https://dl.bintray.com/exactpro/th2'
    }
    jcenter()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

// prevent publish to Bintray from gitlab
tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (publication == publishing.publications.mavenJava)
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
        bintrayPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId rootProject.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', rootProject.name)
                root.appendNode('url', "${vcs_url}")
            }
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
        //Nexus repo for publish from gitlab
        maven {
            name = 'nexusRepository'
            credentials {
                username = "${nexus_user}"
                password = "${nexus_password}"
            }
            url = "${nexus_url}"
        }
    }
}

bintray {
    user = "${bintray_user}"
    key = "${bintray_key}"
    publications = ['bintrayPublication']
    pkg {
        repo = 'th2'
        name = rootProject.name
        userOrg = 'exactpro'
        licenses = ['Apache-2.0']
        vcsUrl = "${vcs_url}"
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
    publish = true
}

dependencies {
    api platform('com.exactpro.th2:bom:2.10.1')

    implementation 'com.exactpro.th2:common:3.4.1-th2-1571-664465879'
    implementation 'com.exactpro.th2:codec:3.1.2-+'

    implementation "com.exactpro.sf:sailfish-core:${sailfishVersion}"

    implementation 'org.slf4j:slf4j-log4j12'
    implementation 'org.slf4j:slf4j-api'

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.7.9'
    implementation group: 'com.athaydes.rawhttp', name: 'rawhttp-core', version: '2.4.1'

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version
}

application {
    mainClassName 'com.exactpro.th2.codec.MainKt'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

serviceLoader {
    serviceInterface 'com.exactpro.th2.codec.api.IPipelineCodec'
    serviceInterface 'com.exactpro.th2.codec.api.IPipelineCodecSettings'
    serviceInterface 'com.exactpro.th2.codec.api.IPipelineCodecSettingsTypeProvider'
}
